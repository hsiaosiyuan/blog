##基本正则表达式

Vim 是一个文本编辑器，这就意味着你的大部分 Vimscript 代码将会专注于文本。Vim 对于正则表达式有很强大的支持，但是它一样有很多的怪癖。

将下面的文本输入到缓冲区：

```vim
max = 10

print "Starting"

for i in range(max):
    print "Counter:", i

print "Done"
```

这些文本将会用于去练习 Vimscript 的正则表达式支持情况。它碰巧是 Python 代码，不过如果你不懂 Python 的话不用担心。这只是一个例子。

我将假设你知道了一些关于正则的基本知识。如果你不知道的话，你应该停止阅读本书，开始阅读 Zed Shaw 的 [努力学习正则](http://regex.learncodethehardway.org/)。当你完成了那个再回来。

##搜索

将你的光标放到文件的首行，然后运行下面的命令：

```vim
/print
```

当你在输入每一个字母的时候，Vim 将对第一行中与它们对应的进行高亮。当你按下回车去执行搜索的时候，所有的 `print` 都将会高亮，并且你的光标将会移动到第一匹配的内容。

现在试一试下面的命令：

```vim
:execute "normal! gg/print\<cr>"
```

这将会先到达文件的第一行然后执行一个搜索 `print` 的操作，然后将我们放置到第一个匹配的内容。这里使用了 `:execute "normal! ..."`，我们在上一节中已经看过了。

要到达第二个匹配的内容，你只需要在字符串的结尾部分追加更多的命令。运行下面的命令：

```vim
:execute "normal! gg/print\<cr>n"
```

Vim 会将光标放置到第二个 `print` 上（并且所有的匹配内容都会高亮）。

让我们试一试反方向的。运行下面的命令：

```vim
:execute "normal! G?print\<cr>"
```

这次我们通过 `G` 移动到文件的底部，然后使用 `?` 去反向搜索。

所有这些搜索命令应该看起来是熟悉的 -- 我们正主要通过它们去让你熟悉 `:execute "normal! ..." ` 的方式，因为这将让你在你的 Vimscript 代码中可以做任何你在一般 Vim 使用中所做的事情。

##魔法（Magic）

`/` 和 `?` 命令实际上接收的是正则表达式，而不只是字符字面量。运行下面的命令：

```vim
:execute "normal! gg/for .+ in .+:\<cr>"
```

Vim 解释说没有找到与模式想匹配的内容！我之前告诉过你 Vim 在搜索中支持正则表达式，那么这里发生了什么？试试下面的命令：

```vim
:execute "normal! gg/for .\\+ in .\\+:\<cr>"
```

这里 Vim 和我们预期的一样高亮了 "for" 循环。在继续前进之前，花一分钟思考下到底哪里发生了改变。记住 `execute` 接收一个字符串。

我们为什么那样做的答案主要由两个原因：
- 首先，`execute` 接收一个字符串，所以我们使用的两个连续的反斜杠在到达 `normal!` 的时候就变成了一个。
- Vim 有四种不同的模式去解析正则表达式！默认的模式 `+` 前面的反斜杠使得它表示 “字面的加号” 而不是 “一个以上的在其之前的字符”。

如果你直接在 Vim 中运行它们的话会看起来简单一点。输入下面的命令并按下回车：

```vim
/print .\+
```

可以看到 `\+` 的魔法生效了。那样使用两个反斜杠的原因只是因为我们需要将模式字符串传递给 `execute`。

##字符串字面量

我们在字符串那一节中提到过，Vim 运行你使用单引号去定义一个可以直接代表字符串本身意义的 “字符串字面量“。比如，这个字符串是四个字节长的 `'a\nb'`。

我们可以使用字符串字面量去避免输入那些连续的两个反斜杠吗？继续之前思考一两分钟，因为结果可能会比想象的要复杂。

试着运行下面的命令（注意这次的单引号和单个反斜杠）：

```vim
:execute 'normal! gg/for .\+ in .\+:\<cr>'
```

Vim 将会移动到了首行，但是没有将你带到第一个匹配的地方。这是你所期望的吗？

这个命令不能工作的原因是因为我们需要模式中的 `\<cr>` 去跳脱成一个回车以此去执行一个搜索。因为我们在一个字符字面量中，它就等于在普通模式下输入了 `/for .\+ in .\+:\<cr>`，这显然不是我们想要的。

可以还有希望！记得 Vim 允许你去连接字符串，所以对于大量的命令组合我们可以通过将它们分块连接以达到易读的目的。运行下面的命令：

```vim
:execute "normal! gg" . '/for .\+ in .\+:' . "\<cr>"
```

这会先将三个更小的字符串连接，然后在将结果发送给 `execute`，这样我们会正则部分使用了字符串字面量，而再其他地方使用了普通的字符串。

##Very Magic

你可能想知道 Vimscript 的四种不同的正则解析模式，以及它们与你使用过的语言，比如Python，Perl，Ruby 之间有什么不同。如果你真的非常想知道的话，你可以去查看它们的文档，但是如果你不想被搞晕的话，最好是继续看下去。

运行下面的命令： 

```vim
:execute "normal! gg" . '/\vfor .+ in .+:' . "\<cr>"
```

我们再次将匹配模式分开，并将命令的剩余部分使用它们的字符串字面量形式，并且这次我们以 `\v` 作为匹配模式的开头。这告诉 Vim 使用它的 “very magic”正则解析模式，此模式和你在其他语言中使用过的非常像。

如果你简单的将所有你的正则表达式都以 `\v` 开头的话，那么你不必担心 Vimscript 的其他三种疯狂的正则模式了。

##练习

仔细阅读 `:help magic`。 


阅读 `:help pattern-overview` 去查看各种 Vim 支持的正则。看到字符类部分就可以停止了。

阅读 `:help match`。动手试试几次 `:match Error /\v.../`。

编辑你的 `~/.vimrc` 去添加一个映射，使其使用 `match` 去高亮尾随的空白。一个比较好的按键可能是 `<leader>w`。

添加另一个映射，它可以删除匹配的内容（比如 `<leader>W`）。

添加一个普通模式下的映射，它可以在你无论何时开始搜索的时候自动的帮你插入 `\v`。如果你牢记了 Vim 的按键映射，那么这是非常简单的，你只需要告诉它当你使用映射按键的时候需要按下哪些键。

将 `hlsearch` 和 `incsearch` 选项添加到你的 `~/.vimrc` 文件中，无论你喜欢与否。

阅读 `:help nohlsearch`。注意那是一个命令，而不是 `hlsearch` 的关闭模式！

在 `~/.vimrc` 文件中添加一个映射，使其可以”停止高亮最后一次的搜索“
